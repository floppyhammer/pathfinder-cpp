#version 430
//#version 310 es

// Initializes the tile maps.

precision highp float;

#define TILE_FIELD_NEXT_TILE_ID             0u
#define TILE_FIELD_FIRST_FILL_ID            1u
#define TILE_FIELD_BACKDROP_ALPHA_TILE_ID   2u
#define TILE_FIELD_CONTROL                  3u

layout(local_size_x = 64) in;

layout (std140, binding = 2) uniform bUniform {
    int uPathCount;
    int uTileCount;
    int pad0;
    int pad1;
};

restrict readonly layout(std430, binding = 0) buffer bTilePathInfo {
    // x: tile upper left, 16-bit packed x/y
    // y: tile lower right, 16-bit packed x/y
    // z: first tile index in this path
    // w: color/ctrl/backdrop word
    uvec4 iTilePathInfo[];
};

restrict writeonly layout(std430, binding = 1) buffer bTiles {
    // [0]: next tile ID (initialized to -1)
    // [1]: first fill ID (initialized to -1)
    // [2]: backdrop delta upper 8 bits, alpha tile ID lower 24 (initialized to 0, -1 respectively)
    // [3]: color/ctrl/backdrop word
    uint iTiles[];
};

void main() {
    uint tileIndex = gl_GlobalInvocationID.x;
    if (tileIndex >= uint(uTileCount)) return;

    uint lowPathIndex = 0u, highPathIndex = uint(uPathCount);
    int iteration = 0;
    while (iteration < 1024 && lowPathIndex + 1u < highPathIndex) {
        uint midPathIndex = lowPathIndex + (highPathIndex - lowPathIndex) / 2u;
        uint midTileIndex = iTilePathInfo[midPathIndex].z;
        if (tileIndex < midTileIndex) {
            highPathIndex = midPathIndex;
        } else {
            lowPathIndex = midPathIndex;
            if (tileIndex == midTileIndex) break;
        }
        iteration++;
    }

    uint pathIndex = lowPathIndex;
    uvec4 pathInfo = iTilePathInfo[pathIndex];

    ivec2 packedTileRect = ivec2(pathInfo.xy);
    ivec4 tileRect = ivec4(
    (packedTileRect.x << 16) >> 16,
    packedTileRect.x >> 16,
    (packedTileRect.y << 16) >> 16,
    packedTileRect.y >> 16);

    uint tileOffset = tileIndex - pathInfo.z;
    uint tileWidth = uint(tileRect.z - tileRect.x);
    ivec2 tileCoords = tileRect.xy + ivec2(tileOffset % tileWidth, tileOffset / tileWidth);

    iTiles[tileIndex * 4u + TILE_FIELD_NEXT_TILE_ID] = ~0u;
    iTiles[tileIndex * 4u + TILE_FIELD_FIRST_FILL_ID] = ~0u;
    iTiles[tileIndex * 4u + TILE_FIELD_BACKDROP_ALPHA_TILE_ID] = 0x00ffffffu;
    iTiles[tileIndex * 4u + TILE_FIELD_CONTROL] = pathInfo.w;
}
